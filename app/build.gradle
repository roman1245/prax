apply plugin: 'com.android.application'

android {
    compileSdkVersion 24
    buildToolsVersion '24.0.1'

    defaultConfig {
        applicationId "xyz.kandrac.library"
        testApplicationId "kandrac.xyz.library.test"
        minSdkVersion 16
        targetSdkVersion 24
        versionCode 23
        versionName "1.2.4-nogo"

        // Database authorities definitions
        buildConfigField 'String', 'DATABASE_AUTHORITY', '"xyz.kandrac.library"'
        buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', '"xyz.kandrac.library.files"'
        buildConfigField 'String', 'GOOGLE_BOOKS_API_URL', '"https://www.googleapis.com"'
        buildConfigField 'String', 'LIBRARY_API_URL', '"http://kandrac.xyz"'
        buildConfigField 'String', 'APP_PUBLIC_KEY', '""'

        vectorDrawables.useSupportLibrary = true
        manifestPlaceholders = [databaseAuthority    : "xyz.kandrac.library",
                                fileProviderAuthority: "xyz.kandrac.library.files"]
    }

    signingConfigs {
        //noinspection GroovyMissingReturnStatement
        release {
            if (System.getenv("KANDRAC_KEYSTORE") != null)
                storeFile file(System.getenv("KANDRAC_KEYSTORE"))
            if (System.getenv("KANDRAC_KEYSTORE_PASS") != null)
                storePassword System.getenv("KANDRAC_KEYSTORE_PASS")
            if (System.getenv("LIBRARY_KEY_ALIAS") != null)
                keyAlias System.getenv("LIBRARY_KEY_ALIAS")
            if (System.getenv("LIBRARY_KEY_PASS") != null)
                keyPassword System.getenv("LIBRARY_KEY_PASS")
        }
    }

    buildTypes {

        release {
            //noinspection GroovyAssignabilityCheck
            signingConfig signingConfigs.release
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            buildConfigField 'String', 'GOOGLE_API_KEY', '"AIzaSyB856kGT7z6lYo_99pikNEGs1jFaCCltxs"'
        }

        debug {
            applicationIdSuffix '.debug'

            // Database authorities definitions
            buildConfigField 'String', 'DATABASE_AUTHORITY', '"xyz.kandrac.library.debug"'
            buildConfigField 'String', 'GOOGLE_API_KEY', '"AIzaSyBhGenifD9XgRW8q-rzEKyo3KlplHAf8J8"'
            buildConfigField 'String', 'FILE_PROVIDER_AUTHORITY', '"xyz.kandrac.library.files.debug"'

            manifestPlaceholders = [databaseAuthority    : "xyz.kandrac.library.debug",
                                    fileProviderAuthority: "xyz.kandrac.library.files.debug"]
        }
    }

    productFlavors {
        pro {
            applicationIdSuffix ".pro"
        }
        free {
            applicationIdSuffix ".free"
        }
    }
}

ext {
    supportLibVersion = '24.2.0'
}

repositories {
    mavenCentral()
}

dependencies {

    // project dependencies
    compile files('libs/opencsv-3.7.jar')
    compile project(':barcode')

    // compat libraries
    compile "com.android.support:appcompat-v7:${supportLibVersion}"
    compile "com.android.support:design:${supportLibVersion}"
    compile "com.android.support:recyclerview-v7:${supportLibVersion}"
    compile "com.android.support:cardview-v7:${supportLibVersion}"
    compile "com.android.support:gridlayout-v7:${supportLibVersion}"

    // helper libraries
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.okhttp3:logging-interceptor:3.3.1'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'

    // gms and fire-base
    compile 'com.google.android.gms:play-services:9.4.0'
    compile 'com.google.firebase:firebase-database:9.4.0'
    compile 'com.firebaseui:firebase-ui:0.5.1'

    // test dependencies
    testCompile 'junit:junit:4.12'
}

apply plugin: 'com.google.gms.google-services'