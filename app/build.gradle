apply plugin: 'com.android.application'

android {
    compileSdkVersion 23
    buildToolsVersion '23.0.2'

    defaultConfig {
        applicationId "xyz.kandrac.library"
        testApplicationId "kandrac.xyz.library.test"
        minSdkVersion 16
        targetSdkVersion 23
        versionCode 9
        versionName "1.1.4"

        // Database authorities definitions
        buildConfigField 'String', 'DATABASE_AUTHORITY', '"xyz.kandrac.library"'
        buildConfigField 'String', 'SNK_DATABASE_AUTHORITY', '"xyz.kandrac.library.snk"'

        manifestPlaceholders = [databaseAuthority   : "xyz.kandrac.library",
                                snkDatabaseAuthority: "xyz.kandrac.library.snk"]
    }

    signingConfigs {
        release {
            if (System.getenv("KANDRAC_KEYSTORE") != null)
                storeFile file(System.getenv("KANDRAC_KEYSTORE"))
            if (System.getenv("KANDRAC_KEYSTORE_PASS") != null)
                storePassword System.getenv("KANDRAC_KEYSTORE_PASS")
            if (System.getenv("LIBRARY_KEY_ALIAS") != null)
                keyAlias System.getenv("LIBRARY_KEY_ALIAS")
            if (System.getenv("LIBRARY_KEY_PASS") != null)
                keyPassword System.getenv("LIBRARY_KEY_PASS")
        }
    }

    buildTypes {

        release {
            minifyEnabled true
            signingConfig signingConfigs.release
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }

        debug {
            applicationIdSuffix '.debug'

            // Database authorities definitions
            buildConfigField 'String', 'DATABASE_AUTHORITY', '"xyz.kandrac.library.debug"'
            buildConfigField 'String', 'SNK_DATABASE_AUTHORITY', '"xyz.kandrac.library.snk.debug"'

            manifestPlaceholders = [databaseAuthority   : "xyz.kandrac.library.debug",
                                    snkDatabaseAuthority: "xyz.kandrac.library.snk.debug"]
        }
    }

    productFlavors {
        pro {
            applicationIdSuffix ".pro"
        }
        free {
            applicationIdSuffix ".free"
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.android.support:recyclerview-v7:23.1.1'
    compile 'com.android.support:cardview-v7:23.1.1'
    compile 'com.android.support:design:23.1.1'
    compile 'com.jakewharton:butterknife:7.0.1'
    compile 'com.google.android.gms:play-services:8.4.0'
    compile 'com.squareup.picasso:picasso:2.5.2'
    compile 'com.squareup.retrofit:retrofit:2.0.0-beta2'
    compile 'com.squareup.retrofit:converter-gson:2.0.0-beta2'
    compile 'com.android.support:gridlayout-v7:23.1.1'
}
